ruleset manageSensors {
  meta {
    use module io.picolabs.subscription alias Subscriptions
    
    shares __testing, getSensors, getTemperatures
    
    logging on
  }

  global {
    threshold = 80
    sms = 8014626297
    eci = -1
    init = {"init":-1};
    
    getSensors = function() {
      Subscriptions:established("Tx_role","sensor").defaultsTo(init)
      // ent:sensors.defaultsTo(init); //name, id
    }
  
    // getTemperatures = function() {
    //   Subscriptions:established("Tx_role","sensor").defaultsTo(init).map(function(x) {
    //       getTemps(x{"Tx"}) 
    //   })
    // }
    
    getTemperatures = function() {
      ent:temps.defaultsTo(init)
    }
    
    __testing = { 
        "queries": [ 
          { "name": "getSensors", "args": [ ] },
          { "name": "getTemperatures", "args": [ ] }
        ],
        "events": [ 
          { "domain": "sensor", "type": "newSensor","attrs": ["name"] },
          { "domain": "sensor", "type": "unneededSensor", "attrs": ["name"] },
          { "domain": "sensor", "type": "introduction", "attrs" : ["eci"] },
          { "domain": "sensor", "type": "tempsUpdate", "attrs" : [] },
          { "domain": "sensor", "type": "tempsResponse", "attrs" : ["temps"] },
          { "domain": "sensor", "type": "tempsReset", "attrs" : [] }
        ]
    }
    
    updProfile = defaction(eci, name, threshold, sms) {
      base_url = <<http://localhost:8080/sky/event/>> + eci + <</5/sensor/profile_updated>>
        http:post(base_url, form =
                {"name":name,
                "threshold":threshold,
                "sms":sms
        });
    }
    
    getTemps = function(eci) {
      base_url = <<http://localhost:8080/sky/cloud/>> + eci + <</temperature_store/temperatures>>;
      http:get(base_url){"content"}
    }
  }
  
  
  rule destroySensorError {
    select when sensor unneededSensor
    pre {
      notExists = ent:sensors{event:attr("name")} == null;
    } 

    if notExists then
      send_directive("error", {"doesn't exist": event:attr("name")});
  }
  
  rule createSensorError {
    select when sensor newSensor
    pre {
      exists = ent:sensors{event:attr("name")} != null
    } 

    if exists then
      send_directive("error", {"already exists": event:attr("name")});
  }
  
  rule createSensor {
    select when sensor newSensor
    pre {
      notExists = ent:sensors{event:attr("name")} == null
    }
    
    if notExists then
      send_directive("sensor", {"created": event:attr("name")});

  
    fired {
      ent:sensors := ent:sensors.defaultsTo(init).put(event:attr("name"), eci);
      raise wrangler event "child_creation"
        attributes { 
          "name": event:attr("name"), 
          "rids": ["temperature_store", "wovyn_base", "sensor_profile"]};
    }
  }
  
  rule created {
    select when wrangler child_initialized
   
    every {
      send_directive("sensor", {"finished creating": event:attr("rs_attrs"){"name"}});
      updProfile(event:attr("eci"),event:attr("rs_attrs"){"name"}, threshold, sms);
    }
    
    always {
      ent:sensors{event:attr("rs_attrs"){"name"}} := event:attr("eci");
      raise wrangler event "subscription" attributes
         { "name" : event:attr("rs_attrs"){"name"},
           "Rx_role": "manager",
           "Tx_role": "sensor",
           "channel_type": "subscription",
           "wellKnown_Tx" : event:attr("eci")
         }
    }
  }
  
  rule destroySensor {
    select when sensor unneededSensor
    pre {
      exists = ent:sensors{event:attr("name")} != null
    } 

    if exists then
       send_directive("sensor", {"destroyed": event:attr("name")});

    
    fired {
      raise wrangler event "child_deletion"
        attributes {"name": event:attr("name")};
      clear ent:sensors{event:attr("name")};
    }
  }
  
  rule sensorIntroduction {
    select when sensor introduction
      // introduce mischief pico to sensor pico
      every {
        send_directive("sensor", {"introducing": event:attr("eci")});
        event:send(
          {
            "eci": event:attr("eci"), "eid": "subscription",
            "domain": "wrangler", "type": "subscription",
            "attrs": { 
               "Rx_role": "sensor",
               "Tx_role": "manager",
               "channel_type": "subscription",
               "wellKnown_Tx": "3kx3Us11EVLXiwweFdrccP"
            } 
          }
         )
      }
  }
  
  rule auto_accept {
    select when wrangler inbound_pending_subscription_added
  
    send_directive("say", {"subscription": "auto accepted"});
    
    fired {
      raise wrangler event "pending_subscription_approval"
        attributes event:attrs
    }
  }
  
  rule sensor_violation {
    select when sensor threshold_violation
    pre {
      mess = "temperature violation: " + event:attr("violation");
    }
    fired {
      raise sensor event "notification"
        attributes {
          "notification": mess 
        }
      }
  }
  
  rule updateTemps {
    select when sensor tempsUpdate
      foreach Subscriptions:established("Tx_role","sensor") setting (x)
    pre {
    }
    every {
      send_directive("updating", {"sub": x{"Tx"}});
      event:send(
            {
              "eci": x{"Tx"}, "eid": "5",
              "domain": "sensor", "type": "tempsRequest",
              "attrs": {
                "reportId" : ent:reportId.defaultsTo(0),
                "requesterTx" : x{"Rx"}
              } 
            }
      );
    }
    always {
      ent:temps{ent:reportId} := ent:temps{ent:reportId}.defaultsTo(init)
        .put("temperature_sensors", Subscriptions:established("Tx_role","sensor").length());
      ent:reportId := (ent:reportId.defaultsTo(0) + 1) % 5 on final;
    }
  }
  
  rule handle_response {
    select when sensor tempsResponse
    send_directive("responding", {"temps": event:attr("temps")});
    always {
      ent:temps{[event:attr("reportId"), "temperatures"]} := ent:temps{[event:attr("reportId"), "temperatures"]}.defaultsTo(init)
        .put(event:attr("responderRx"), event:attr("temps"));
      ent:temps{[event:attr("reportId"), "responding"]} := ent:temps{[event:attr("reportId"), "responding"]}.defaultsTo(0) + 1;
    }
  }
  
  rule clearTemps {
    select when sensor tempsReset
    always {
      ent:temps := init;
    }
  }
  
  // rule sensor_tempsRequest {
  //   select when sensor tempsRequest
  //   pre {
  //   }
  //   send_directive("say", {"sub": Subscriptions:established()});
  //   always {
  
  //   }
  // }
}
